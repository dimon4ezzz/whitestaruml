"Start Symbol" = <Grammar Attribute Name>
"Case Sensitive" = False                
               
! -------------------------------------------------
! Character Sets
! -------------------------------------------------
{Visibility Chars} = [+-#~]
{Restricted Chars} = {All Space} + {Visibility Chars} ! Restricted Chars should not start or end an indentifier as they may collide with other symbols                   
{Boundary String Chars} = {All Letters} +  {Number} + {Euro Sign} + [_,;/\.&@$%*"`’'']
{String Chars} = {Boundary String Chars} + {Restricted Chars}

! -------------------------------------------------
! Terminals
! -------------------------------------------------
id = {Boundary String Chars}+ ( {Restricted Chars}+ {Boundary String Chars}+ )* ( {Restricted Chars}* '<' {String Chars}* '>' | {Restricted Chars}* '[' {String Chars}* ']'  | {Restricted Chars}* '(' {String Chars}* ')' )? 

! -------------------------------------------------
! Rules
! -------------------------------------------------

! The grammar starts below
<Grammar Attribute Name> ::= <attribute_expression> 
<attribute_expression> ::= <stereotype_part> <visibility> <attribute_name> <type_expression_part> <initial_value_part>
                           
<stereotype_part> ::= '<<' <stereotype> '>>'
                   |  <>

<stereotype> ::= id

<visibility> ::= '+' | '#' | '-' | '~'
              | <> 

<attribute_name> ::= id
                  
<type_expression_part> ::= ':' <type_expression>
                        | <>

<type_expression> ::= <type_id>                  
                        
<initial_value_part> ::= '=' <initial_value>
                      | <>
       
<type_id> ::= id

<initial_value> ::= id 
        

